model PairwiseStable_Initial
uses "mmxprs", "mmsystem"

parameters
   DATAFILE    = "../Instances/CSPLPO_50_5_12_1.dat"
   OUTFILE     = "../outputs/Results_PairwiseStable_Initial.txt"
   SOLFILE     = "../outputs/Solution_PairwiseStable_Initial.txt"
   TIME_LIMIT  = 3600
end-parameters


!--------------------------
! Instance data
!--------------------------
declarations
   nI   : integer
   nJ   : integer
   nIns : integer
end-declarations

initializations from DATAFILE
   nI
   nJ
   nIns
end-initializations

declarations
   rI = 1..nI
   rJ = 1..nJ

   CostJ  : array(rJ) of real
   CostIJ : array(rI,rJ) of real
   CapJ   : array(rJ) of integer
   Pref   : array(rI,rJ) of integer

   x : array(rI,rJ) of mpvar
   y : array(rJ) of mpvar

   fobj : linctr

   t0, timeRL, timeMIP : real
   objRL, objMIP       : real
   bestBound           : real
   nodes               : integer
   nRows, nCols, nNnz  : integer
   jAssigned           : integer
end-declarations

initializations from DATAFILE
   CostJ
   CostIJ
   Pref
   CapJ
end-initializations


!--------------------------
! Decision variables
!--------------------------
forall(i in rI, j in rJ) x(i,j) is_binary
forall(j in rJ) y(j) is_binary


!--------------------------
! Constraints
!--------------------------
forall(i in rI)
   sum(j in rJ) x(i,j) = 1

forall(i in rI, j in rJ)
   x(i,j) <= y(j)

forall(j in rJ)
   sum(i in rI) x(i,j) <= CapJ(j) * y(j)

! Pairwise stability constraints
! (No blocking pairs / blocking customers)
forall(i in rI, j in rJ, jj in rJ | Pref(i,j) < Pref(i,jj))
   CapJ(j) * (y(j) + x(i,jj) - 1) <= sum(ii in rI | Pref(ii,j) < Pref(ii,jj)) x(ii,j)


!--------------------------
! Objective function
!--------------------------
fobj := sum(j in rJ) CostJ(j)*y(j) + sum(i in rI, j in rJ) CostIJ(i,j)*x(i,j)


!--------------------------
! Solve LP relaxation
!--------------------------
t0 := gettime
minimize(XPRS_LIN, fobj)
objRL := getobjval
timeRL := gettime - t0


!--------------------------
! Solve MIP
!--------------------------
setparam("XPRS_MAXTIME", -TIME_LIMIT)

t0 := gettime
minimize(fobj)
timeMIP := gettime - t0

nRows := getparam("XPRS_ROWS")
nCols := getparam("XPRS_COLS")
nNnz  := getparam("XPRS_ELEMS")

nodes     := getparam("XPRS_NODES")
bestBound := getparam("XPRS_BESTBOUND")
objMIP    := getparam("XPRS_MIPOBJVAL")


!--------------------------
! Append summary results
!--------------------------
fopen(OUTFILE, F_OUTPUT + F_APPEND)
   writeln("PairwiseStable_Initial,", DATAFILE, ",",
           nI, ",", nJ, ",", CapJ(1), ",", nIns, ",",
           objRL, ",", timeRL, ",",
           objMIP, ",", bestBound, ",", timeMIP, ",",
           nodes, ",", nCols, ",", nRows, ",", nNnz)
fclose(F_OUTPUT)


!--------------------------
! Save detailed solution
!--------------------------
fopen(SOLFILE, F_OUTPUT + F_APPEND)
   writeln("MODEL: PairwiseStable_Initial")
   writeln("DATAFILE: ", DATAFILE)
   writeln("nI=", nI, "  nJ=", nJ, "  nIns=", nIns)
   writeln("OBJ(MIP): ", objMIP, "   BestBound: ", bestBound, "   Time: ", timeMIP)
   writeln

   writeln("OPEN FACILITIES:")
   forall(j in rJ | getsol(y(j)) > 0.5)
      writeln("   Facility ", j)

   writeln
   writeln("ASSIGNMENTS:")
   forall(i in rI) do
      jAssigned := 0
      forall(j in rJ | getsol(x(i,j)) > 0.5) jAssigned := j
      writeln("   Customer ", i, " -> ", jAssigned)
   end-do
   writeln("--------------------------------------------------")
fclose(F_OUTPUT)

end-model