model CyclicStable_Pro
uses "mmxprs", "mmsystem"

parameters
   DATAFILE    = "../Instances/CSPLPO_50_5_12_1.dat"
   OUTFILE     = "../outputs/Results_CyclicStable_Pro.txt"
   SOLFILE     = "../outputs/Solution_CyclicStable_Pro.txt"
   TIME_LIMIT  = 3600
end-parameters


!--------------------------
! Instance data
!--------------------------
declarations
   nI    : integer
   nJ    : integer
   nIns  : integer
   nR    : integer
   CapsJ : integer
end-declarations

initializations from DATAFILE
   nI
   nJ
   nIns
end-initializations

nR := nJ

declarations
   rI = 1..nI
   rJ = 1..nJ
   rR = 1..nR

   CostJ  : array(rJ) of real
   CostIJ : array(rI,rJ) of real
   CapJ   : array(rJ) of integer
   Pref   : array(rI,rJ) of integer

   x : array(rI,rJ) of mpvar
   y : array(rJ) of mpvar
   u : array(rJ,rR) of mpvar

   fobj : linctr

   t0, timeRL, timeMIP : real
   objRL, objMIP       : real
   bestBound           : real
   nodes               : integer
   nRows, nCols, nNnz  : integer
   jAssigned           : integer
end-declarations

initializations from DATAFILE
   CostJ
   CostIJ
   Pref
   CapJ
end-initializations

CapsJ := CapJ(1)


!--------------------------
! Decision variables
!--------------------------
forall(i in rI, j in rJ) x(i,j) is_binary
forall(j in rJ) y(j) is_binary
forall(j in rJ, r in rR) u(j,r) is_binary


!--------------------------
! Constraints
!--------------------------
forall(i in rI)
   sum(j in rJ) x(i,j) = 1

forall(i in rI, j in rJ)
   x(i,j) <= y(j)

forall(j in rJ)
   sum(r in rR) u(j,r) <= y(j)

sum(j in rJ) u(j,1) <= 1
forall(r in 2..nJ)
   sum(j in rJ) u(j,r) <= sum(j in rJ) u(j,r-1)

forall(j in rJ)
   sum(i in rI) x(i,j) <= (CapJ(j)-1)*y(j) + sum(rr in rR) u(j,rr)

forall(j in rJ)
   CapJ(j)*sum(r in rR) u(j,r) <= sum(i in rI) x(i,j)

! Cyclic/coalition stability constraints (Pro)
forall(i in rI, j in rJ, r in 1..nJ-1)
   x(i,j) + sum(rr in 1..r-1) u(j,rr) + sum(jj in rJ | Pref(i,jj) < Pref(i,j)) u(jj,r) <= 1 + y(j)

forall(i in rI, j in rJ)
   y(j) - sum(r in rR) u(j,r) <= sum(jj in rJ | Pref(i,jj) <= Pref(i,j)) x(i,jj)


!--------------------------
! Objective function
!--------------------------
fobj := sum(j in rJ) CostJ(j)*y(j) + sum(i in rI, j in rJ) CostIJ(i,j)*x(i,j)


!--------------------------
! Solve LP relaxation
!--------------------------
t0 := gettime
minimize(XPRS_LIN, fobj)
objRL := getobjval
timeRL := gettime - t0


!--------------------------
! Solve MIP
!--------------------------
setparam("XPRS_MAXTIME", -TIME_LIMIT)

t0 := gettime
minimize(fobj)
timeMIP := gettime - t0

nRows := getparam("XPRS_ROWS")
nCols := getparam("XPRS_COLS")
nNnz  := getparam("XPRS_ELEMS")

nodes     := getparam("XPRS_NODES")
bestBound := getparam("XPRS_BESTBOUND")
objMIP    := getparam("XPRS_MIPOBJVAL")


!--------------------------
! Append summary results
!--------------------------
fopen(OUTFILE, F_OUTPUT + F_APPEND)
   writeln("CyclicStable_Pro,", DATAFILE, ",",
           nI, ",", nJ, ",", CapsJ, ",", nIns, ",",
           objRL, ",", timeRL, ",",
           objMIP, ",", bestBound, ",", timeMIP, ",",
           nodes, ",", nCols, ",", nRows, ",", nNnz)
fclose(F_OUTPUT)


!--------------------------
! Save detailed solution
!--------------------------
fopen(SOLFILE, F_OUTPUT + F_APPEND)
   writeln("MODEL: CyclicStable_Pro")
   writeln("DATAFILE: ", DATAFILE)
   writeln("nI=", nI, "  nJ=", nJ, "  nIns=", nIns)
   writeln("OBJ(MIP): ", objMIP, "   BestBound: ", bestBound, "   Time: ", timeMIP)
   writeln

   writeln("OPEN FACILITIES:")
   forall(j in rJ | getsol(y(j)) > 0.5)
      writeln("   Facility ", j, "  (u=", sum(r in rR) getsol(u(j,r)), ")")

   writeln
   writeln("ASSIGNMENTS:")
   forall(i in rI) do
      jAssigned := 0
      forall(j in rJ | getsol(x(i,j)) > 0.5) jAssigned := j
      writeln("   Customer ", i, " -> ", jAssigned)
   end-do
   writeln("--------------------------------------------------")
fclose(F_OUTPUT)

end-model